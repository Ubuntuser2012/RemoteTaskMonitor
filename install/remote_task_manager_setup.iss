; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
; http://www.jrsoftware.org/ishelp/index.php?topic=scriptfunctions

#define MyAppName "Remote Task Monitor"
#define MyAppVersion "7.10a"
#define MyAppURL "http://www.remotetaskmonitor.com/"
#define MyAppExeName "rtmonitor.exe"
#define LIBSDIR "C:\work\libs\build\win32\bin"
#define QTDIR "C:\msys64\mingw32\bin"
#define VCREDIST "Microsoft_Visual_C++_2015_Redistributable_Update_3_RC_vc_redist.x86.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{68EDE8C5-A7F8-4242-A142-81A107E4FA25}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename=RemoteTaskMonitorSetup{#MyAppVersion}
SetupIconFile=..\res\remotecpu.ico
Compression=lzma
SolidCompression=yes
LicenseFile=..\..\..\work.web\www.remotetaskmonitor.com\license.txt

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: "..\out\win32\rtmonitor.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\out\win32\pcmon*.exe"; DestDir: "{app}"; Flags: ignoreversion
;Source: "..\out\wince\cemon*.exe"; DestDir: "{app}\wince"; Flags: ignoreversion
Source: "..\out\thomas\cemon*.exe"; DestDir: "{app}\wince"; Flags: ignoreversion
Source: "..\..\Python32\libs\python32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\python\script1.py"; DestDir: "{app}\python"; Flags: ignoreversion
Source: "..\Lib\*.*"; DestDir: "{app}\Lib"; Flags: ignoreversion recursesubdirs
Source: "instructions.pdf"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "showinstructions.bat"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "..\..\..\work.web\www.remotetaskmonitor.com\license.txt"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "..\..\..\work.web\www.remotetaskmonitor.com\credits.txt"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: "{#LIBSDIR}\{#VCREDIST}"; DestDir: "{app}"; AfterInstall: InstallVCRedistPackage

Source: "{#LIBSDIR}\libcurl.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libjpeg-9.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\liblibgd.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libpng16-16.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libzlib.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\zlib1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libcrypto-1_1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libssl-1_1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libeay32.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#LIBSDIR}\libfreetype-6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#QTDIR}\libgcc_s_dw2-1.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#QTDIR}\libstdc++-6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#QTDIR}\libwinpthread-1.dll"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; 
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "explorer"; Parameters: "{app}\wince"; Description: "Open install directory"; Flags: postinstall nowait skipifsilent
;Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait runascurrentuser postinstall skipifsilent
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait runascurrentuser postinstall skipifsilent
Filename: "{app}\showinstructions.bat"; Flags: skipifsilent

[Messages]
FinishedLabel=Please copy and run one of the cemon files to your WinCE device, or linuxrtm to your Linux device.
ClickFinish=Then run rtmonitor on your computer, type the IP address of your device and click the connect button.

[Code]
procedure InitializeWizard;
begin
  WizardForm.LicenseAcceptedRadio.Checked := True;
end;

procedure InstallVCRedistPackage;
var 
  vcRedistVersion : String;
  ResultCode: Integer; 
begin
  if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\Wow6432Node\Microsoft\DevDiv\VC\Servicing\14.0\RuntimeMinimum',
     'Version', vcRedistVersion) then
    begin
      // Successfully read the value
      // MsgBox('Microsoft Visual Studio Redistributable Package was detected. Version: ' + vcRedistVersion, mbInformation, MB_OK);
    end
  else
    begin
      MsgBox('{#MyAppName} will now install Microsoft Redistributable Package', mbInformation, MB_OK);
      if not Exec(ExpandConstant('{app}\{#VCREDIST}'), '', '', SW_SHOWNORMAL,
        ewWaitUntilTerminated, ResultCode)
      then
        MsgBox('Microsoft Redistributable Package failed to run!' + #13#10 +
          SysErrorMessage(ResultCode), mbError, MB_OK);
      end
end;